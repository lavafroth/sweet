// TODO: Add the full sets
modifier = {
    ^"super"
  | ^"ctrl"
  | ^"alt"
  | ^"shift"
}

key = { ^"enter" | ^"return" | ASCII_ALPHANUMERIC }

escape_lf   = _{ "\\\n" }
WHITESPACE  = _{ " " | "\t" | escape_lf }
not_newline = _{ !NEWLINE ~ !escape_lf ~ ANY }

shorthand_deny  = { NEWLINE | "{" | "," | "}" | "-" }
shorthand_allow = { "\\," | "\\\\" | "\\{" | "\\}" | "\\-" }

keybind_component = _{ !shorthand_deny ~ (shorthand_allow | keybind) }

key_range    =  { keybind_component ~ "-" ~ keybind_component }
key_or_range = _{ key_range | keybind_component }

shorthand = {
    "{" ~ ((keybind_component ~ "," ~ key_or_range) | key_range) ~ ("," ~ key_or_range)* ~ "}"
}

modifier_in_shorthand = _{ !(NEWLINE | "{" | "," | "}") ~ modifier }
modifier_shorthand    =  { "{" ~ modifier_in_shorthand ~ ("," ~ modifier_in_shorthand)+ ~ "}" }

omission            =  { "_" }
modifier_omit       = _{ omission | (modifier_in_shorthand ~ concat) }
modifier_omit_shorthand =  { "{" ~ modifier_omit ~ ("," ~ modifier_omit)* ~ "}" }

send                             =  { "~" }
on_release                       =  { "@" }
keybind                          =  { send? ~ on_release? ~ key }
modifier_or_shorthand_without_concat = _{ modifier | modifier_shorthand }
modifier_or_shorthand                = _{ modifier_or_shorthand_without_concat ~ concat }
concat                           = _{ "+" }

comment = _{ WHITESPACE* ~ "#" ~ not_newline* }

command_composite     = _{ !shorthand_deny ~ (shorthand_allow | ANY) }
command_component     =  { command_composite+ }
visible_composite     =  { command_composite }
range          =  { visible_composite ~ WHITESPACE* ~ "-" ~ WHITESPACE* ~ visible_composite }
command_component_and =  _{ WHITESPACE* ~ "," ~ WHITESPACE* ~ (range | command_component) }

inside_braces      =  _{ (command_component ~ command_component_and+) | (range ~ command_component_and*) }
command_with_brace =  { "{" ~ inside_braces ~ "}" }
command            = ${ NEWLINE ~ WHITESPACE+ ~ (command_with_brace | command_component)* }

trigger = _{(modifier_or_shorthand | modifier_omit_shorthand)* ~ (keybind | shorthand)}

binding = {
    trigger ~ comment? ~ command
}

unbind = { "ignore" ~ trigger }

content = _{ comment | unbind | binding | NEWLINE }

main = {
    SOI ~ content* ~ EOI
}
